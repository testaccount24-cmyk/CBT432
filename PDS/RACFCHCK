*
* THIS PROGRAM "RACHECK-ES" A DATASET OR A GENERAL RESOURCE
*
* RETURN CODE : 0 = AUTHORIZED
*               4 = RACF DOES NOT KNOW THIS RESOURCE
*               8 = NOT AUTHORIZED
*
* EXAMPLE 1 :
*
*          CALL  RACFCHCK,(PARAM),VL
* PARAM    DS    0F
* LENGTH   DC    AL1(8)                    1 B = LENGTH OF CLASS
* CLASS    DC    CL8'FACILITY'             8 C = NAME OF RACF CLASS
* RESOURCE DC    CL256'ZZZZ.ABCD'          VAR LENGTH : RESOURCE NAME
*
* EXAMPLE 2 :
*
*          CALL  RACFCHCK,(PARAM),VL
* PARAM    DS    0F
* LENGTH   DC    AL1(7)                    1 B = LENGTH OF CLASS
* CLASS    DC    CL8'DATASET'              8 C = NAME OF RACF CLASS
* RESOURCE DC    CL256'SYS1.RACFDS'        VAR LENGTH : RESOURCE NAME
*
**********************************************************************
* START OF PROGRAM                                                   *
**********************************************************************
RACFCHCK CSECT
RACFCHCK AMODE 31
RACFCHCK RMODE ANY
         BAKR  R14,0                  ESTABLISH A BASE REGISTER
         LR    R12,R15                REG BASE
         USING RACFCHCK,R12
         B     START
         DC    C'RACFCHCK-&SYSDATC-&SYSTIME-(C)TH-FALISSARD, 2000'
START    DS    0H
* OBTAIN WORK STORAGE
         L     R4,0(R1)               SAVE PARM POINTER
         USING ZONEPARM,R4
         STORAGE OBTAIN,LENGTH=WORKAREALEN GET SOME STORAGE
         LR    R10,R1                  SET WORK REG
         USING WORKAREA,R10
*
* RACHECK ON DATATSET CLASS : AVOID DISCRETE PROFILE CASE
*
         CLI   ZPLCLASS,X'07'            LENGTH OF RACF CLASS >= 7 ?
         BL    NOTDS                     NO, NOT A DATASET
         CLC   ZPCLASSE(7),=CL7'DATASET'  DATASET CLASS ?
         BNE   NOTDS                     NO, NOT A DATASET
         MVC   RAC_E_UP(RAC_L_UPL),RAC_L_UP    COPY RACROUTE SKELETON
         RACROUTE REQUEST=AUTH,ATTR=READ,                              X
               ENTITY=ZPPROFIL,                RACF PROFILE            X
               CLASS=ZPLCLASS,                 RACF CLASS              X
               RELEASE=1.9.2,                                          X
               WORKA=WORKA,MF=(E,RAC_E_UP),DSTYPE=M   (MODEL PROFILE)
         LR    R6,R15                      SAVE RETURN CODE
         B     FREE
*
* RACHECK ON GENERAL RESOURCE
*
NOTDS    DS    0H
         MVC   RAC_E_UP(RAC_L_UPL),RAC_L_UP    COPY RACROUTE SKELETON
         RACROUTE REQUEST=AUTH,ATTR=READ,                              X
               ENTITY=ZPPROFIL,                RACF PROFILE            X
               CLASS=ZPLCLASS,                 RACF CLASS              X
               RELEASE=1.9.2,                                          X
               WORKA=WORKA,MF=(E,RAC_E_UP)
         LR    R6,R15                      SAVE RETURN CODE
         B     FREE
*
* FREE OUR WORK ZONE
*
FREE     DS    0H
         LR    R1,R10                  POINT TO WORKAREA
         STORAGE RELEASE,ADDR=(R1),LENGTH=WORKAREALEN
         LR    R15,R6                  SET RETURN CODE
         PR    ,                       RESTORE REGS AND RETURN
*        .
RAC_L_UP RACROUTE REQUEST=AUTH,MF=L,ATTR=READ,RELEASE=1.9.2
RAC_L_UPL EQU *-RAC_L_UP
*
         LTORG
*
* DSECTS
*
ZONEPARM DSECT
ZPLCLASS DS    CL1           LENGTH OF CLASS OF RESOURCES
*                            (FOR EXAMPLE X'08' FOR 'FACILITY')
ZPCLASSE DS    CL8           CLASS OF RESOURCES
ZPPROFIL DS    CL40          RACF PROFILE, PADDED WITH BLANKS
*
* WORK AREA
*
WORKAREA DSECT
RAC_E_UP RACROUTE REQUEST=AUTH,MF=L,ATTR=UPDATE,RELEASE=1.9.2
EXITCODE DS  F                         EXIT CODE
*    DATA AREAS
WORKA    DS    0F
         DS    CL512        LENGTH OF WORK AREA.
         DS    CL100        SUPPLEMENT
WEND     EQU   *             END OF WORK AREA.
WORKAREALEN EQU   *-WORKAREA
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END
