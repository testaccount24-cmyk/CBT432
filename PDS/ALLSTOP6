An automated tool to stop address spaces

I wrote the following program for those who still do not have
automated operations software at their disposal to trigger
automatically actions requiring a considerable number of MVS
or JES commands to be issued. It was originally dedicated to
stopping all active tasks before IPLing, relieving our operators
from the chore of searching for outstanding reply numbers,
entering scores of commands in the correct order, etc.

This program can also be used to stop and restart critical
address spaces like VTAM or JES 'on the fly' without even
having to go to the console room. The program reads
parameters from the SYSIN file and verifies whether the
address space (first parameter) is active - should this be the
case a JES or MVS command (second parameter), which can
be a reply, is issued in order to stop the job or the started task.
The keyword 'WAIT' makes ALLSTOP wait for a while,
'PJES2' instructs it to stop JES2 (if there are no active jobs),
and the 'MVS' keyword issues the 'Z EOD' command
provided that JES has stopped.

The operators can monitor events as they happen, for the
program sends them high-intensity messages which are rolled
up as the stopping proceeds.


JCL procedure to implement ALLSTOP

//*  Command to stop all products :
//*  -  S ALLSTOP,SUB=MSTR,M=STOPMVS
//*  Command to stop and restart JES :
//*  -  S ALLSTOP,SUB=MSTR,M=STOPJES,P=BYPASS  (NO WTOR TO CONFIRM)
//ALLSTOP PROC M=ALLSTOP,P=
//ALLSTOP EXEC PGM=ALLSTOP,TIME=1440,PARM=&P
//SYSIN   DD DISP=SHR,DSN=SYS1.PARMLIB(&M)


STOPJES EXAMPLE

* 1) SYS1.PARMLIB(STOPJES) : TO STOP AND RESTART JES2
* JES2 is stopped "on the fly" and hot-started, so is other software
* that is closely linked to JES2
* - S ALLSTOP,M=STOPJES
CADSDISP REPLY 99STOPCADS
JES328X P JES328X
SPOOLMAN P SPOOLMAN
* Cancel all presently running AFP prints ...
FSS0 C FSS0
FSS1 C FSS1
   etc
FSS9 C FSS9
* Wait till everyone stops
CADSDISP WAIT
JES328X WAIT
SPOOLMAN WAIT
* Immediate shutdown of JES2
ALLSTOP $PJES2,ABEND
ALLSTOP WAIT 05
JES2 REPLY EXIT
* Wait for JES2 to stop
JES2 WAIT
* Of course program "ALLSTOP" can be used to start tasks,
* not only to stop them ...
* Restart JES2
ALLSTOP S JES2,PARM='WARM,NOREQ'
* Restart the other software
ALLSTOP S CADSDISP
ALLSTOP S JES328X
ALLSTOP S SPOOLMAN
ALLSTOP C ALLSTOP                 SUICIDE SINCE OUR JOB IS DONE


STOPMVS EXAMPLE

* 2) SYS1.PARMLIB(STOPMVS) : TO STOP ALL JOBS/STCS THAT ARE RUNNING NOW
* EXAMPLE IS GIVEN WITH JES2, MVS/ESA 3.1.3 (+ IDMS, + CICS, ETC.)
* warning : all parameters on a line must be separated by 1 blank
*           (and only one)
*=======================================================================
* LIST OF STATEMENTS (THEY MUST BEGIN AT COLUMN 1) :                   =
* *                  -> comment line (ignored)                         =
* NOSTOP xxxx        -> no need to stop 'xxxx' before stopping JES2    =
* jjjjj REPLY zzzz   -> issue 'REPLY nn,zzzz' to stop job jjjjj        =
* jjjjj yyyy         -> issue the MVS command 'yyyy' to stop job jjjjj =
*                    -> (if jjjjj not found, the command is not issued)=
* jjjjj WAIT         -> if job jjjjj is still active, wait for it to   =
*                    -> stop (if it is not active, statement ignored)  =
* jjjjj WAIT nn      -> if job jjjjj is still active, wait nn seconds  =
*                    -> (if it is not active, the statement is ignored)=
* WAIT nn            -> wait nn seconds before going on                =
* $PJES2             -> issue $p JES2 (provided there is no active job =
*                    ->   except jobs kept in the 'NOSTOP' list)       =
* MVS                -> issue Z EOD (provided JES2 has been stopped)   =
*=======================================================================
* List of tasks that won't stop, and need not (system address spaces)
NOSTOP *MASTER*
NOSTOP CATALOG
NOSTOP PCAUTH
NOSTOP TRACE
NOSTOP GRS
NOSTOP DUMPSRV
NOSTOP CONSOLE
NOSTOP ALLOCAS
NOSTOP SMF
NOSTOP LLA
NOSTOP VLF
NOSTOP DLF
NOSTOP SMS
NOSTOP ALLSTOP
NOSTOP JESL
NOSTOP INIT
NOSTOP RASP
NOSTOP IOSAS
NOSTOP SMXC
NOSTOP SYSBMAS
* stop all inits, if there is one still active
INIT $PI
* warn TSO users
TSO SE '** ATTENTION, MVS IS STOPPING ... **'
TSO F TSO,USERMAX=0
* route all messages to the MVS master console
CONSOLE V 005,CONSOLE,ROUT=ALL
CONSOLE K S,DEL=RD,L=01
CONSOLE K S,DEL=RD,L=02
* deactivation of 3990-3 cache controller
CONSOLE S SETCACHE,M=INACT320
* draining DLF (hiperbatch)
DLF F DLF,MODE=D
*  STOP ALL IDMS
IDMS06 REPLY SHUTDOWN
IDMS30 REPLY SHUTDOWN
IDMS50 REPLY SHUTDOWN
IDMS80 REPLY SHUTDOWN
IDMS1 REPLY SHUTDOWN
*  STOP ALL CICS
CICS1 F CICS1,CEMT P SHUT
CICB F CICB,CEMT P SHUT
CICM F CICM,CEMT P SHUT
CICX F CICX,CEMT P SHUT
CICSTES F CICSTES,CEMT P SHUT
CICSTST2 F CICSTST2,CEMT P SHUT
CICSMVS F CICSMVS,CEMT P SHUT
CICSPRD1 F CICSPRD1,CEMT P SHUT
*  STOP THE REMAINING PRODUCTS
JES2 $PLINE(1-*)
TMONLINE P TMONLINE
PDSMAN P PDSMAN
TELEX REPLY P LINE ALL
CADSDISP REPLY 99STOPCADS
CADSTEST REPLY 99STOPCADS
VCSS1 P VCSS1
VCSS2 P VCSS2
TEMPUS P TEMPUS
NPMV1R4 P NPMV1R4
EPILOGC P EPILOGC
CSFIA P CSFIA
GTMOSIB P GTMOSIB
GTMOSIC P GTMOSIC
CSFIF P CSFIF
RESOLVE P R
SDSI P SDSI
MIM P MIM
DFHSM F DFHSM,STOP
DFHSMTST F DFHSMTST,STOP
CMF P CMF
OMS P OMS
DLF P DLF
J328X P J328X
J328XTST P J328XTST
CLCONF P CLCONF
CICSM P CICSM
OPCW P OPCW
OPCA P OPCA
FS00 P FS00
FS01 P FS01
FS02 P FS02
FS03 P FS03
FS04 P FS04
FS05 P FS05
FS06 P FS06
FS07 P FS07
FS08 P FS08
ASCCT C ASCCT
* may we insist ...? (shutting down test CICS immediately)
CICSTES F CICSTES,CEMT P SHUT IMM
CICSTST2 F CICSTST2,CEMT P SHUT IMM
*  STOP THE SESSION MANAGER
CLVTAM SE '** CLVTAM WILL STOP WITHIN 10 SECONDS **'
CLVTAM WAIT 10
CLVTAM P CLVTAM
TELEX WAIT 02
TELEX REPLY P PGM
* RETRY STOPPING IT, IN CASE IT IS RELUCTANT...
CLVTAM WAIT 5
CLVTAM P CLVTAM
*  STOP TSO
TSO SE '** TSO WILL BE OFF IN 10 SECONDS EXACTLY **'
TSO WAIT 10
TSO SE '** ATTENTION, IMMEDIATE SHUTDOWN OF TSO **'
TSO P TSO
TSO WAIT 03
TSO REPLY FSTOP
TSO WAIT 03
TSO REPLY U
TSO WAIT 02
TSO WAIT         <-- WAIT TILL TSO STOPS
ANONETV REPLY CLOSE IMMED
ACONETV REPLY CLOSE IMMED
FSS0 C FSS0
FSS1 C FSS1
   etc
FSS20 C FSS20
CICS1 WAIT       <--- WAIT FOR THIS CICS TO STOP
CICB WAIT        <--- WAIT FOR THIS CICS TO STOP
CICM WAIT        <--- WAIT FOR THIS CICS TO STOP
CICX WAIT        <--- WAIT FOR THIS CICS TO STOP
CICSTES WAIT     <--- WAIT FOR THIS CICS TO STOP
CICSMVS WAIT     <--- WAIT FOR THIS CICS TO STOP
CICSTST2 WAIT    <--- WAIT FOR THIS CICS TO STOP
CICSPRD1 WAIT    <--- WAIT FOR THIS CICS TO STOP
ANONVSSI C ANONVSSI
ACONVSSI C ACONVSSI
CONSOLE P GTF
MIM REPLY LOCAL
OMS P OMS
* NOW WE CAN STOP THE NETWORK
NET Z NET,CANCEL
NET WAIT          <-- WAIT FOR VTAM TO STOP
*  STOPPING JES2
$PJES2                    <=== STOPPING JES2 IF NO JOB STILL ACTIVE
JES2 WAIT
*  THE END
MVS                       <=== Z EOD IF JES2 HAS BEEN STOPPED


PROGRAM ALLSTOP

*--- This program can stop all jobs and stcs currently active  ---*
*    according to parameters you indicate (ddname SYSIN)          *
*    ===> IT MUST BE LINK-EDITED WITH AC=1                        *
*--- "SPACES" MACRO SETS A ZONE TO ALL SPACES                  ---*
         MACRO
&NLA     SPACES  &ZONE
         MVI   &ZONE,C' '                  FIRST BYTE SET TO SPACE
         MVC   &ZONE+1(L'&ZONE-1),&ZONE    PROPAGATE TO ALL BYTES
         MEND
* "SYSJOB" MACRO VERIFIES WHETHER A JOB IS ACTIVE                 *
         MACRO
&NLA     SYSJOB  &JOBNAME=
&NLA     DS    0H
.* IF JOB IS ACTIVE, WE OBTAIN ITS ASCB ADDRESS IN REG 1, AND ITS
.* ASID NUMBER IN REG 0 (RIGHT HALFWORD)
          STM   R5,R8,SVRG&SYSNDX   SAVE REGISTERS 5 THROUGH 8
          L     R15,16              CVT ADDRESS
          PUSH  USING
          USING CVT,R15
          L     R6,CVTASVT          LOAD ASVT ADDRESS
          DROP  R15
          USING ASVT,R6
          L     R5,ASVTMAXU
          LA    R6,ASVTENTY-4       R6=ADDRESS OF 1ST ASCB MINUS 4
LSVT&SYSNDX DS  0H
          LTR   R5,R5               MAXUSER = 0 ?
          BZ    NTFN&SYSNDX         YES, ASVT LOOKUP ENDED ->NOT FOUND
          BCTR  R5,0                MAXUSER - 1
          LA    R6,4(R6)            NEXT ASVT ENTRY
          USING ASCB,R7
          L     R7,0(R6)            R7=ASCB ADDRESS
          TM    0(R6),X'80'         AVAILABLE ASID ?
          BO    LSVT&SYSNDX         ASID NOT AVAILABLE, LET'S SEE NEXT
          CLC   =C'ASCB',ASCBASCB   VERIFYING ACRONYM
          BNE   LSVT&SYSNDX
          L     R1,ASCBJBNI         LOAD ADDRESS OF JOBNAME
          LTR   R1,R1
          BNZ   LJBN&SYSNDX
          L     R1,ASCBJBNS         LOAD ADDRESS OF STC
LJBN&SYSNDX CLC 0(8,R1),&JOBNAME    IS IT THAT JOB ?
          BNE   LSVT&SYSNDX
.* JOB WAS FOUND
          LH    R0,ASCBASID         LOAD ASID NUMBER
          LR    R1,7                PUT ASCB ADDRESS IN REG 1
          XR    R15,R15             RETURN CODE = 0
          B     XSVT&SYSNDX
NTFN&SYSNDX DS  0H
          LA    R15,12              RETURN CODE = 12, JOB DOESN'T EXIST
          B     XSVT&SYSNDX
.* JOB WAS NOT FOUND
SVRG&SYSNDX DS 4F                   4 WORDS TO SAVE REGISTERS 5,6,7,8
XSVT&SYSNDX DS 0H
          LM    R5,R8,SVRG&SYSNDX
          DROP  R6,R7
          POP   USING
          MEND
* "$MVSCMD" MACRO ISSUES AN MVS COMMAND                           *
         MACRO
.*
&NLA     $MVSCMD  &CMD
&NLA     DS   0H
         LA   1,L'&CMD
         MVC  MGCRCMD(L'&CMD),&CMD
         STH  1,MGCRLEN
         MODESET MODE=SUP,KEY=ZERO
         LA   1,MGCRLEN
         XR   0,0
         SVC  34                  MGCR MACRO TO ADD COMMAND IN QUEUE
         MODESET MODE=PROB,KEY=NZERO
         MEXIT
         MEND
* $REPLY MACRO GETS THE REPLY NUMBER OF THE WTOR ASSOCIATED TO A JOB  *
         MACRO
.*
.*  ASID    = BINARY WORD CONTAINING THE ASID NUMBER OF THE ADDRESS-
.*            SPACE ASSOCIATED TO THE REPLY NUMBER  (INPUT)
.*  REPLYID = 2 CHAR-ZONE THAT WILL GET THE REPLY NUMBER  (OUTPUT)
.*
.*  CHAINING OF CONTROL BLOCKS : CVT -> UCMBASE -> ORE -> ORE -> ...
.*
&NLA     $REPLY &REPLYID=,&ASID=
.*
&NLA     DS    0H
         MNOTE '** THIS MACRO USES REG 1, 2, AND DSECTS CVT,UCM,ORE **'
         PUSH  USING
         L    R1,16               LOAD ADDRESS OF CVT
         USING CVT,R1
         L    R1,CVTCUCB          LOAD ADDRESS OF UCM BASE
         DROP R1
         USING UCM,R1
         LTR  R1,R1
         BZ   BAD&SYSNDX          ADDRESS OF UCM BASE = 0
         L    R2,UCMRPYQ          LOAD ADDRESS OF 1ST ORE
         DROP R1
         USING OREF,R2
         LTR  R2,R2               ADDRESS OF  ORE = 0 ?
         BZ   BAD&SYSNDX
         MODESET MODE=SUP,KEY=ZERO
LOOP&SYSNDX  DS  0H               LOOPING
         CLC  OREASID,&ASID+2     OUR ASID ?
         BE   YESB&SYSNDX         YES, QUIT THE LOOP
         L    R2,ORELKP           TAKE ADDRESS OF NEXT ORE
         LTR  R2,R2               ADDRESS OF NEXT ORE = 0 ?
         BZ   NOTF&SYSNDX         YES, AT END IN CHAIN OF ORES
         B    LOOP&SYSNDX
YESB&SYSNDX  DS  0H               FOUND
         MVC  &REPLYID.(2),OREID       KEEP REPLY NUMBER
         MODESET MODE=PROB,KEY=NZERO
         XR   R15,R15
         B    END&SYSNDX
NOTF&SYSNDX  DS  0H               NOT FOUND
         MODESET MODE=PROB,KEY=NZERO
BAD&SYSNDX  DS  0H                UNHAPPY END
         LA   R15,16
END&SYSNDX DS 0H               END
         DROP R2
         POP  USING
         MEND
* DSECTS NEEDED                                                   *
         CVT  DSECT=YES                ,    CVT
         IHAASCB                       ,    ASCB
         IHAASVT                       ,    ASVT
         IHAORE                        ,    OPERATOR REPLY ELEMENT
         IEECUCM                       ,    UCM
         DCBD    DSORG=PS,DEVD=DA           DCB
ALLSTOP  CSECT
* HOUSEKEEPING
         STM  R14,R12,12(R13)     SAVE REGISTERS
         LR   R12,R15
         USING ALLSTOP,R12        R12 IS BASE REGISTER
         ST   R13,SAVE+4
         LA   R13,SAVE
* IF PARAMETER "BYPASS" WAS CODED, THEN NO WTOR WILL BE ISSUED
         L    R5,0(R1)
         CLC  0(2,R5),=H'6'       ONLY "BYPASS" IS SUPPORTED
         BNE  NOPARM
         CLC  =C'BYPASS',2(R5)
         BNE  NOPARM
         B    CONFIRM             "BYPASS" WAS CODED, NO WTOR
NOPARM   DS   0H
* REPLY TO CONFIRM
WTOR1    XC   ECB,ECB
         LA   R6,REPLY
         LA   R8,1                LENGTH OF REPLY ZONE = 1 CHARACTER
         WTOR '** STOPPING ALL PRODUCTS BEFORE IPLING, ENTER C TO CONFIX
               RM, T TO TERMINATE',(6),(8),ECB
         WAIT ECB=ECB
* ANALYZING THE REPLY
         CLI  REPLY,C'C'          R XX,C : STOPPING MUST BE DONE
         BE   CONFIRM
         CLI  REPLY,C'T'          R XX,T : EXIT
         BE   THEEND
         WTO  '** INVALID REPLY, PLEASE REENTER **'
         B    WTOR1
CONFIRM  DS   0H
         LA   R11,TABNOSTP        TABLE OF JOBS/TASKS NOT TO BE STOPPED
* VERIFY IF //SYSIN WAS ALREADY OPENED
         LA   R1,SYSIN
         USING IHADCB,R1
         TM   DCBOFLGS,DCBOFOPN   SYSIN ALREADY OPEN ?
         BO   ALREOPEN
         DROP R1
         OPEN (SYSIN,(INPUT))     OPEN //SYSIN FILE
ALREOPEN DS   0H
LOOPIN   GET  SYSIN,ZONE
* ANALYSE PARAMETERS
         SPACES  REPLZONE
         CLI  ZONE,C'*'           LINE OF COMMENTS ?
         BE   LOOPIN
         CLC  =C'NOSTOP ',ZONE    "DON'T STOP" JOB ?
         BE   NOSTOPT
         CLC  =C'WAIT ',ZONE      MUST WE WAIT A WHILE ?
         BE   WAITTIME
         CLC  ZONE(6),$PJES2      MUST WE ALREADY STOP JES2 ?
         BE   TRTJES2
         CLC  =C'MVS ',ZONE       MUST WE ISSUE "Z EOD" ?
         BE   TRTMVS
* PROCESSING A JOBNAME (IN ORDER TO STOP IT CORRECTLY)
         SPACES JOBNAME           SET TO SPACES
         LA   R2,JOBNAME
         LA   R1,ZONE
* LOOP TO GET THE NAME OF THE JOB OR THE STC TO BE STOPPED
LOOPTR   CLI  0(R1),C' '          BLANK ?
         BE   ENDOFZON            YES, END OF ZONE
         MVC  0(1,R2),0(R1)       GET ONE CHARACTER OF JOBNAME
         LA   R1,1(0,R1)          NEXT CHARACTER IN ZONE
         LA   R2,1(0,R2)          NEXT CHARACTER IN OUTPUT ZONE
         B    LOOPTR
ENDOFZON DS   0H
         CLI  JOBNAME,C' '        BLANK PARAMETER
         BE   LOOPIN
* PROCESS ONE JOB
         LA   R1,1(0,R1)
         MVC  REPLZONE(60),0(R1)
         CLC  =C'REPLY ',0(R1)    JOB CAN BE STOPPED VIA REPLY
         BE   SENDREPL
* THE JOB CAN BE STOPPED BY AN MVS COMMAND
         SYSJOB  JOBNAME=JOBNAME
         LTR  R15,R15
         BNZ  NOJOB
         ST   R0,ASID
* IF  "JOBNAME WAIT" WAS CODED, AND JOB IS ACTIVE, LET'S WAIT
LABEL31  CLC  =C'WAIT ',REPLZONE
         BNE  LABEL11
* IF  "JOBNAME WAIT NN" WAS CODED, LET'S WAIT 'NN' SECONDS
         CLC  =C'WAIT  ',REPLZONE
         BE   WAITPURE                'JOBNAME WAIT'
         MVC  INT+4(2),REPLZONE+5     WAIT TIME
         B    PREPWAIT
* WAITING FOR EVER, UNTIL THE JOB STOPS
WAITPURE BAL  R9,WAITJOBS
         B    LOOPIN
* ISSUE A COMMAND TO STOP THE JOB
LABEL11  $MVSCMD REPLZONE
         B    LOOPIN
* THE JOB MUST BE STOPPED BY ISSUING A REPLY
SENDREPL DS   0H
         SPACES REPLZONE
         MVC  REPLZONE(40),6(R1)
         SYSJOB  JOBNAME=JOBNAME
         LTR  R15,R15
         BNZ  NOJOB
         ST   R0,ASID
         $REPLY REPLYID=RR,ASID=ASID   WHAT REPLY IS PENDING FOR JOB?
         LTR  R15,R15
         BNZ  NOREPLY
* ISSUE THE REPLY COMMAND
         SPACES  COMMAND                      BLANKING OUT COMMAND ZONE
         MVC  COMMAND(2),=C'R '                      "R
         MVC  COMMAND+2(2),RR                           NN
         MVI  COMMAND+4,C','                              ,
         MVC  COMMAND+5(L'REPLZONE),REPLZONE               XXXXXXXX"
         $MVSCMD COMMAND
         B    LOOPIN
* STORE IN A TABLE THE NAMES OF THE ADDRESS SPACES NEVER TO BE STOPPED
NOSTOPT  DS   0H
         CLI  0(R11),X'FF'
         BE   ENDNOSTP
         MVC  0(8,R11),ZONE+7           STORE JOBNAME
         LA   R11,8(0,R11)              NEXT ENTRY IN TABLE
ENDNOSTP B    LOOPIN
* JOB NOT FOUND (WARNING MESSAGE)
NOJOB    MVC  WTO4+24(8),JOBNAME
WTO4     WTO  '**ALLSTOP - JOB ???????? IS NOT ACTIVE'
         B    LOOPIN
* REPLY NOT FOUND (WARNING MESSAGE)
NOREPLY  MVC  WTO5+24(8),JOBNAME
WTO5     WTO  '**ALLSTOP - JOB ???????? HAS NO REPLY PENDING'
         B    LOOPIN
* ENTERING WAIT STATE
WAITTIME DS   0H
         MVC  INT+4(2),ZONE+5
PREPWAIT MVC  WTO1+36(2),INT+4
WTO1     WTO '**ALLSTOP -  WAITING DURING ?? SECONDS **',DESC=(11)
         ST   R1,MSGIDWTO
         STIMER WAIT,,DINTVL=INT        WAITING...
NOSTIM1  L    R1,MSGIDWTO
         LTR  R1,R1
         BZ   LOOPIN
         DOM  MSG=(1)                   SUPPRESS THE MESSAGE
         B    LOOPIN
* BEFORE STOPPING JES2, WE VERIFY THAT NO JOB IS STILL RUNNING    *
TRTJES2  DS   0H
         L    R15,16              CVT ADDRESS
         USING CVT,R15
         L    R6,CVTASVT          LOAD ASVT ADDRESS
         DROP R15
         USING ASVT,R6
         L    R5,ASVTMAXU         R5=MAXUSER
         LA   R6,ASVTENTY-4       R6=ADDRESS OF 1ST ASCB MINUS 4
LSVT1    DS   0H
         LTR  R5,R5               MAXUSER = 0 ?
         BZ   NTFN1               YES, AT END IN ASVT -> NOT FOUND
         BCTR R5,0                MAXUSER - 1
         LA   R6,4(R6)            NEXT ASVT ENTRY
         USING ASCB,R7
         L    R7,0(R6)            R7=ASCB ADDRESS
         TM   0(R6),X'80'         AVAILABLE ASID ?
         BO   LSVT1               ASID NOT AVAILABLE, SKIP IT
         L    R1,ASCBJBNI         LOADING ADDRESS OF JOBNAME
         LTR  R1,R1
         BNZ  LJBN1
         L    R1,ASCBJBNS         LOADING ADDRESS OF STCNAME
LJBN1    DS   0H
* VERIFY WHETHER THIS JOB MUST BE STOPPED
         CLC  JES,0(R1)           JES IS NOT YET TO BE STOPPED...
         BE   LSVT1
         LA   R10,TABNOSTP
LOOP1    CLI  0(R10),X'FF'
         BE   NOTALL
         CLC  0(8,R1),0(R10)      JOBNAME FOUND IN TABLE ?
         BE   LSVT1               YES, SKIP IT
         LA   R10,8(0,R10)
         B    LOOP1
* NOW WE ARE SURE ALL JOBS REQUIRED WERE STOPPED, JES2 MAY BE STOPPED
NTFN1    DS   0H
         $MVSCMD $PJES2           STOPPING JES2 ITSELF
         B    LOOPIN
         DROP R6,R7
* ALL JOBS REQUIRED WERE NOT STOPPED, WE MUST WAIT FOR THEM
NOTALL   DS   0H
         MVC  WTO2+43(8),0(R1)
WTO2     WTO  '**ALLSTOP WILL NOT STOP JES2 WHILE ???????? IS ACTIVE'
         BAL  R9,WAITJOBS
         B    TRTJES2             LET'S TRY AND STOP JES AGAIN
* BEFORE ISSUING Z EOD, WE VERIFY THAT JES HAS BEEN STOPPED       *
TRTMVS   DS   0H
         SYSJOB  JOBNAME=JES
         LTR  R15,R15
         BNZ  ZEOD
         WTO  '**ALLSTOP - Z EOD WILL NOT BE DONE WHILE JES IS ACTIVE'
ENDIN    CLOSE (SYSIN)
         B    WTOR1
ZEOD     DS   0H
         WTO  '**ALLSTOP PROCEDURE COMPLETED (Z EOD) **',DESC=(11)
         $MVSCMD HALTEOD
         CLOSE (SYSIN)
THEEND   L    R13,4(R13)
         RETURN  (14,12),T,RC=0
* WAITING TILL A JOB HAS STOPPED
WAITJOBS DS   0H
         MVC  WTO3+36(8),JOBNAME
WTO3     WTO  '**ALLSTOP - WAITING FOR JOB ???????? TO STOP',DESC=(11)
         ST   R1,MSGIDWTO
LOOPWAI  STIMER WAIT,,DINTVL=INTWAIT
         SYSJOB JOBNAME=JOBNAME
         LTR  R15,R15
         BZ   LOOPWAI        JOB STILL ACTIVE, WAITING AGAIN...
NOSTIM2  L    R1,MSGIDWTO
         LTR  R1,R1
         BZ   ENDWAITJ
         DOM  MSG=(1)        THE MESSAGE CAN BE ROLLED
ENDWAITJ BR R9
INTWAIT  DC   C'00000500'    WAIT INTERVAL (5 SECONDS)
MSGIDWTO DC   F'0'           ID OF MESSAGE AFTER WTO
SAVE     DS   18F            SAVE AREA
JES      DC   CL8'JES2'
HALTEOD  DC   CL80'Z EOD'
REPLY    DS   X'00'
RR       DS   CL2            REPLY NUMBER
INT      DC   C'00000000'    WAIT INTERVAL (VARIABLE)
ECB      DS   F
ASID     DS   F
SYSIN    DCB  DDNAME=SYSIN,EODAD=ENDIN,MACRF=GM,DSORG=PS
REPLZONE DS   CL80
ZONE     DS   CL80
TABNOSTP DC   30CL8' '      TB OF ADDRESS SPACES THAT NEED NOT BE STOPD
         DC   X'FF'         END INDICATOR
JOBNAME  DC   CL8' '
$PJES2   DC   CL80'$PJES2'
COMMAND  DC   CL80' '
MGCRLEN  DC   H'0'          LENGTH
         DC   H'0'          ROUTCODE
MGCRCMD  DC   CL80' '       COMMAND
         LTORG
         END


Thierry Falissard
Systems Engineer
Banque La Henin (France)                              c Xephon 1993

