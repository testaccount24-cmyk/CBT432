 /* REXX EXEC TO HELP ENABLE PADS MODE                               */
 /* (pads = program access to data sets)                             */
 /*                                                                  */
 /* This exec may be used to install PADS mdoe access to a dataset.  */
 /* It must be adapted.                                              */
 /*                                                                  */
 /* It was built originally for Consul/RACF to access the RACF DB    */
 /* in PADS mode from ISPF.                                          */
 /* The big "plus" of this exec is that it defines all link-list     */
 /* libraries in the "*" profile of class PROGRAM.                   */

 /*------------------------------------------------------------------*/
 /* PARAMETERS TO BE SET                                             */
 /* The file "myfile" is to be accessed by user "padsusr" only       */
 /* using program "mypgm" from loadlib "loadlib" on disk "loadlibv". */
 /*------------------------------------------------------------------*/

 LOADLIB  = 'P390.LOADLIB'                 /* LOADLIB LIBRARY DSNAME */
 LOADLIBV = 'OS39M1'                       /* LOADLIB LIBRARY VOLSER */
                                           /* LOADLIBV = '' IF SMS   */
 MYFILE   = 'IBMUSER.MYFILE'  /* GENERIC PROFILE FOR RACF DB         */
 MYPGM    = 'MYPROGM'         /* program used in pads mode           */
 PADSUSR  = 'P390A'           /* USER OR GROUP TO USE     PADS MODE  */
 catlged  = 'Y'               /* 'Y' if datasets in link-list        */
                              /* are cataloged, any other value if   */
                              /* you are unsure.                     */

 say 'RACF version is' sysvar(syslracf)

 if sysvar(syslracf) >= '2040' then do
        LOADLIBV = ''       /* no volser needed for RACF v2.4       */
        end
 /*------------------------------------------------------------------*/
 /* PRELIMINARY RACF ACTIONS (supposing there is already a profile   */
 /*  covering MYFILE).                                               */
 /*  - Make sure default access to MYFILE is 'none'                  */
 /*  - Make sure RACF program control is active                      */
 /*------------------------------------------------------------------*/

 IF 1 = 1 THEN DO
 ADDRESS TSO "ADDSD '"MYFILE"'  GENERIC  UACC(NONE)"
 say 'Addsd of dataset' myfile 'resulted in rc=' rc
 ADDRESS TSO "ALD '"MYFILE"'  GENERIC  UACC(NONE)"
 say 'Altdsd of dataset' myfile 'resulted in rc=' rc
 say 'Uacc of file' myfile 'set to none, rc=' rc
 /* ACTIVATE PROGRAM CONTROL                                 */
 ADDRESS TSO "SETROPTS WHEN(PROGRAM)"
 SAY     "SETROPTS WHEN(PROGRAM)    RC=" RC
 end

 /*------------------------------------------------------------------*/
 /* DEFINE PROGRAM PROFILES                                          */
 /*------------------------------------------------------------------*/
 IF 1 = 1 THEN DO
         ADDRESS TSO "RDEF  PROGRAM " mypgm ,
                 "ADDMEM('"||LOADLIB||"'/"||LOADLIBV||"/PADCHK)"
         SAY     "RDEF PROGRAM" mypgm "RC=" RC

        END

 /*------------------------------------------------------------------*/
 /* USER-SPECIFIC ACTIONS                                            */
 /* 1) permit use of program                                         */
 /* 2) permit access to MYFILE in pads mode                          */
 /*------------------------------------------------------------------*/
 IF 1 = 1 THEN DO
 /* 1) PERMIT USE OF PROGRAM                                         */
         ADDRESS TSO ,
         "PE" mypgm "CLASS(PROGRAM) ID("PADSUSR") ACCESS(READ)"
         SAY "PERMIT PROGRAM" mypgm "TO USER/GROUP" PADSUSR "RC="RC
 /* 2) PERMIT ACCESS TO MYFILE IN PADS MODE                          */
         /* GRANT PERMIT IN PADS MODE TO THE USER/GROUP              */
         ADDRESS TSO "PE '"MYFILE"' GENERIC" ,
         "WHEN(PROGRAM("mypgm")) ID("PADSUSR")"
         SAY     "PERMIT" MYFILE "TO USER/GROUP" PADSUSR "RC="RC
         ADDRESS TSO "SETR REFRESH GENERIC(DATASET)"
        END

 CMD = 'RALTER '      /* No need to RDEFine * in PROGRAM further on  */
 /*------------------------------------------------------------------*/
 /* DELETE THE GENERIC ENTRY '*' IN CLASS PROGRAM                    */
 /*------------------------------------------------------------------*/

 IF 1 = 0 THEN DO           /* 1 = 0 : we suppose the entry exists   */
        ADDRESS TSO "RDEL PROGRAM *"
        SAY         "RDEL PROGRAM *   RC=" RC
        CMD = 'RDEFINE'      /* RDEFine must be done further on      */
        END

 /*------------------------------------------------------------------*/
 /* REBUILD THE GENERIC ENTRY '*' IN CLASS PROGRAM                   */
 /* RACF-AUTHORIZE ACCESS TO LINKLIST IN ORDER                       */
 /* NOT TO LOAD A 'DIRTY' (NON-CONTROLLED) MODULE                    */
 /*------------------------------------------------------------------*/

 sysres = MVSVAR('SYMDEF','SYSR1')          /* system resident volume*/
                        /* sysres name will be replaced by '******'  */

 say
 IF 1 = 1 THEN DO
     CVT  = STORAGE(10,4)                   /* CVT ADDRESS IN MEMORY */
                                            /* CVT+4DC=CVTLLTA       */
     LLT    = STORAGE(D2X(1244+C2D(CVT)),4) /* LINKLIST TABLE address*/
     NB     = STORAGE(D2X(4+C2D(LLT)),4)    /* Number of entries     */
     NBR    = C2D(NB)                       /* Number of entries     */
     I = 0
     O = 9
     DO WHILE I < NBR
     DSN    = STORAGE(D2X(O+C2D(LLT)),44)   /* DSN of library       */
     dsn    = strip(dsn)
     X      = LISTDSI(DSN)  /* GET DATASET INFORMATION (VOLUME)     */
     SAY  "LINK-LIST:" DSN sysvolume
     vol    = sysvolume
     if     vol = sysres  then vol = "'******'"
     /* Beginning with RACF 2.4, volser may be omitted              */
     if sysvar(syslracf) >= '2040' & catlged = 'Y' then do
        vol = ''            /* no volser needed for RACF            */
        end
     I = I+1
     O = O+45
     /* FIRST PASS may be used to CREATE '*' ENTRY IN PROGRAM CLASS */
     IF   I  >  1  THEN   CMD = 'RALTER '
     /* TAKE INTO ACCOUNT THIS LINK-LIST LIBRARY                    */
     ADDRESS TSO  CMD  "PROGRAM  * " ,
     "ADDMEM('"||DSN||"'/"||VOL||"/NOPADCHK) UACC(READ)"
     SAY CMD "PROGRAM * FOR DSN=" DSN "("VOL") RC=" RC
     END
     END


 /*------------------------------------------------------------------*/
 /* REFRESH PROGRAM PROFILES TO ACTIVATE CHANGES                     */
 /*------------------------------------------------------------------*/
 ADDRESS TSO "SETROPTS REFRESH WHEN(PROGRAM)"
